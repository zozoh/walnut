apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "war"
apply from: "common.gradle"

import org.nutz.lang.Files

group = 'org.nutz'
version = '1.r.61-SNAPSHOT'

description = "一台神奇的计算机"

sourceCompatibility = 1.8
targetCompatibility = 1.8

def version_tag = getDate()
def nutz_version = "1.r.62-SNAPSHOT"

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

sourceSets {
    main{
    	java {
    		srcDirs = ["$projectDir/src"]
    	}
    	resources {
    		srcDirs = ["$projectDir/conf"]
    	}
    }
    test {
    	java {
    		srcDirs "$projectDir/test"
    	}
    }
}

processResources {
    from ('src'){
        exclude '**/*.java';
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
	options.compilerArgs = ["-parameters"]
}

tasks.withType(War) {
	webAppDirName = "WebContent"
}

repositories {
    //mavenLocal()
    maven { url "https://jfrog.nutz.cn/artifactory/jcenter"}
    maven { url "https://jfrog.nutz.cn/artifactory/snapshots"}
}
dependencies {
    compile group: 'org.nutz', name: 'nutz', version: nutz_version
    compile(group: 'org.nutz', name: 'nutz-web', version: nutz_version)
    
    compile group: 'org.apache.commons', name: 'commons-email', version:'1.4'
    compile group: 'org.jsoup', name: 'jsoup', version:'1.8.3'
    compile group: 'org.nutz', name: 'nutzmongo', version:nutz_version
    compile group: 'org.nutz', name: 'nutzwx', version:nutz_version
    compile group: 'org.nutz', name: 'nutz-plugins-qrcode', version:nutz_version
    compile group: 'org.nutz', name: 'nutz-integration-json4excel', version:nutz_version
    compile group: 'org.nutz', name: 'nutz-quartz', version:'1.r.61-SNAPSHOT'
    compile group: 'com.qiniu', name: 'qiniu-java-sdk', version:'7.0.8'
    compile group: 'org.apache.sshd', name: 'sshd-core', version:'1.1.1'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version:'1.7.22'
    compile group: 'cn.jpush.api', name: 'jpush-client', version:'3.2.8'
    compile group: 'org.brickred', name: 'socialauth', version:'4.10'
    compile group: 'org.nutz', name: 'nutz-mipush-sdk', version:'3.0.MOD'
    compile group: 'javax.websocket', name: 'javax.websocket-api', version:'1.1'
    runtime group: 'log4j', name: 'log4j', version:'1.2.17'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
    compile group: 'org.apache.ftpserver', name: 'ftpserver-core', version:'1.1.0'
    compile group: 'cn.apiclub.tool', name: 'simplecaptcha', version: '1.2.2'
    compile group: 'org.subethamail', name: 'subethasmtp', version: '3.1.7'
    compile group: 'org.apache.poi', name: 'poi', version: '3.15'
    compile group: 'org.apache.poi', name: 'poi-scratchpad', version: '3.15'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.15'
    compile group: 'org.apache.poi', name: 'poi-ooxml-schemas', version: '3.15'
    compile group: 'org.apache.poi', name: 'ooxml-schemas', version: '1.3'
}

buildscript {
    repositories {
		maven { url "https://jfrog.nutz.cn/artifactory/jcenter"}
    }

    dependencies {
        classpath 'org.nutz:nutz:1.r.61.r2'
    }
}

task wbuild(dependsOn: classes) << {
	copy {
		from sourceSets.main.output
		into "build/wzip/classes"
		exclude '**/web_local.properties'
	}
	copy {
		from "WebContent"
		into "build/wzip/WebContent"
	}
	copy {
		from "ROOT"
		into "build/wzip/ROOT"
	}
	copy {
		from configurations.runtime
		into "build/wzip/libs"
	}
	
   	def web_props = Files.read(file('build/wzip/classes/web.properties'))
   	web_props = web_props.replaceAll("~/workspace/git/github/walnut", ".")
   	Files.write(file('build/wzip/classes/web.properties'), web_props)
   	
   	def jetty_websocket = '''org.eclipse.jetty.websocket.server.NativeWebSocketServletContainerInitializer
org.eclipse.jetty.websocket.jsr356.server.deploy.WebSocketServerContainerInitializer
   	'''
   	
   	Files.write(file('build/wzip/classes/META-INF/services/javax.servlet.ServletContainerInitializer'), jetty_websocket);
   	
   	tasks.wruns.execute()
}

task wruns << {
	Files.write(file('build/wzip/build.properties'), "walnut.version="+version_tag)

   	def tmpWin32 = '''java -Dfile.encoding=UTF-8 -Dsun.jnu.encoding=UTF-8 -cp "classes;libs/*"'''
   	def tmpUnix = '''touch classes/web_local.properties; java -Dfile.encoding=UTF-8 -Dsun.jnu.encoding=UTF-8 -cp "classes:libs/*"'''
   	tmpWin32 += " org.nutz.web.WebLauncher"
   	Files.write(file('build/wzip/start.bat'), tmpWin32)

   	tmpUnix += " org.nutz.web.WebLauncher"
   	Files.write(file('build/wzip/start.sh'), tmpUnix)
   	file('build/wzip/start.sh').setExecutable(true, false)

   	Files.write(file('build/wzip/stop.sh'), '''#!/bin/bash
		killall java
	''')
   	file('build/wzip/stop.sh').setExecutable(true, false)


   	Files.write(file('build/wzip/run.sh'), '''
cd "$(dirname "$0")"
cp $WUPROOT/web_local.properties classes/web_local.properties
touch classes/web_local.properties
export JAVA_HOME=$WUPROOT/jdk
export PATH=$JAVA_HOME/bin:$PATH
./start.sh
	''')
	file('build/wzip/run.sh').setExecutable(true, false)

   	Files.write(file('build/update'), '''#!/bin/bash
   		killall java
   		rm -r $WUPROOT/$APPNAME.old
   		mv $WUPROOT/$APPNAME $WUPROOT/$APPNAME.old
   		mkdir $WUPROOT/$APPNAME/
   		tar -C $WUPROOT/$APPNAME/ -x -f $APPNAME.tar
   		rm -rf $WUPROOT/$APPNAME.old &
   	''')
   	file('build/update').setExecutable(true, false)
}

task wzip(dependsOn: wbuild) << {
   	task (_wzip, type: Zip) {
   		from "build/wzip/"
   		archiveName 'walnut-'+version_tag+'.zip'
   		destinationDir buildDir
   	}.execute()
}

task wtar(dependsOn: wbuild) << {
   	task (_wtar, type: Tar) {
   		from "build/wzip/"
   		archiveName 'walnut.tar'
   		destinationDir buildDir
   	}.execute()

   	task (_wtar2, type: Tar) {
   		from "build/update"
   		from "build/walnut.tar"
   		archiveName 'walnut-'+version_tag+'.tgz'
   		destinationDir buildDir
   		compression Compression.GZIP
   	}.execute()
}

war {
    rootSpec.exclude("**/jetty-*.jar")
    rootSpec.exclude("**/org.eclipse.jdt*.jar")
    rootSpec.exclude("**/javax.servlet.jsp*.jar")
    rootSpec.exclude("**/javax.servlet-*.jar")
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}
